//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\0\s[0]What do you want me to call you?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnNameTeach]\![set,balloontimeout,-1]\![open,inputbox,OnNameTeach,-1]\e"
}

OnNameTeach
{	
	_tempname = CUTSPACE(reference.raw[0]) //this stores the original name in _tempname
	_name = reference.raw[0] //putting the raw name into _name to do some tests on it
	_name = TOLOWER(_name) //changes it to lowercase
	_name = REPLACE(_name," ","") //removes spaces
	_name = REPLACE(_name,".","") //removes periods
	_name = REPLACE(_name,"-","") //removes dashes
	
	_is_first_boot = 0; if reference1 == "firstboot"; _is_first_boot = 1
	_function = "OnNameTeach"; if _is_first_boot; _function = "OnFirstNameTeach"
	
	if _is_first_boot; "\_l[-10000,-10000]\q[ ,__VERY_FIRST_BOOT__]\_l[0,0]\*\t" //Prevents balloon from timing out if this is during first boot. Also prevents the user from closing out the input box
	--
	if _tempname == username && !_is_first_boot //If the user gives the exact same name as before. The firstboot check has to be there in case they put in 'user'
	{
		"\0\s[0]Alright.\w8 Nevermind then."
	}
	elseif _name == "aiba" || _name == "aiball"
	{
		"\0\s[0]... Yes?\w8 That is my name.\w8 Pick something else, okay?"
		--
		if !_is_first_boot; "\n\n\![*]\q[Cancel,OnConfigMenu,OnNameTeach]" //If changing name through the menu
		--
		"\![open,inputbox,%(_function),-1]"
	}	
	elseif _name == "date" || _name == "kaname" || _name == "kanamedate"
	{
		"\0\s[0]Come on.\w8 I know you're not Date.\w8 Pick something else."
		--
		if !_is_first_boot; "\n\n\![*]\q[Cancel,OnConfigMenu,OnNameTeach]" //If changing name through the menu
		--
		"\![open,inputbox,%(_function),-1]"
	}
	elseif _name == "" //If the user doesn't put in anything
	{
		username = "User"
		
		"\0\s[0]Alright then.\w8 I will just call you User."
	}
    elseif _name == "iris" || _name == "aset"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]%(username)?\w8 Are you a streamer, by chance?"
	}
	elseif _name == "mizuki" || _name == "mizukiokiura"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]Keeping Date in check, $(username)?"
	}	
	elseif (_name == "saito" || _name == "saitosejima") && spoilerToggle == "major"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0].\w8.\w8.\w8.\w8.\w8\n\nNo.\w8 I saw him die.\w8 You're a different %(username), right?"
	} 
	elseif _name == "pewter" || _name == "futa" || _name =="futaamanoma"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]%(username)?\w8 You don't need to push a new software update for me, right?"
	}
	elseif _name == "ota" || _name == "otamatsushita" 
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]%(username)?\w8 I better not find any weird otaku stuff on here..."
	}
	elseif _name == "boss" || ((spoilerToggle == "minor" || spoilerToggle == "major") && (_name == "shizuekuranushi" || _name == "shizue")) 
	{ 
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		
		"\0\s[0]Hey, %(username).\w8 Cleaned up your office at all?"
	}
	else //if it's none of these above names, we can use it. You can add names that just have comments and can still be used, just don't add a \e at the end.
	{
		username = SHIORI3FW.EscapeAllTags(_tempname) //Escape any tags the user might've stuck in there
		"\0\s[0]Ok,\w4 I'll call you %(username) then."
	}
	--
	if !_is_first_boot //If the user was changing their name through the config menu
	{
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //This is continuing from FirstBoot
	{
		"\w8\w8 Good to meet you.\w8\w8 How should I refer to you?\n\n"
		--
		all:{
		"\![*]\q[He/Him/His,OnPronouns,hehimhis,firstboot]\n"
		"\![*]\q[She/Her/Hers,OnPronouns,sheherhers,firstboot]\n"
		"\![*]\q[They/Them/Their,OnPronouns,theythemtheirs,firstboot]\n"
		"\![*]\q[Custom,OnPronouns,custom,firstboot]"
		}
	}
}

OnChangePronoun //Envelope names are the same as the GT template, since I figured most people will be used to them.
{
	"\*\0\s[0]How should I refer to you?"
	--
	all:{
	"\n\n\_q"
	"\![*]\q[He/Him/His,OnPronouns,hehimhis]\n"
	"\![*]\q[She/Her/Hers,OnPronouns,sheherhers]\n"
	"\![*]\q[They/Them/Their,OnPronouns,theythemtheirs]\n"
	"\![*]\q[Custom,OnPronouns,custom]\n\n"
	
	"\![*]\q[Cancel,OnConfigMenu]\e"
	}
}

//custom pronouns code adapted from SmokyCinnamonRoll#7652 on the ukgk discord 
OnPronouns
{

    if reference0 == "custom" {

		"\![raise,OnSubjectPronounTeach,%(reference1)]\e"

    }  	
	else { //one of the boilerplate pronouns picked
	
		if reference0 == "hehimhis" {
		
			pronounsArray = IARRAY 
			pronounsArray[0] = "singular"
			pronounsArray[1] = "he"
			pronounsArray[2] = "him"
			pronounsArray[3] = "his"
			pronounsArray[4] = "his"
			pronounsArray[5] = "himself"
		
		}
		elseif reference0 == "sheherhers" {
		
			pronounsArray = IARRAY 
			pronounsArray[0] = "singular"
			pronounsArray[1] = "she"
			pronounsArray[2] = "her"
			pronounsArray[3] = "her"
			pronounsArray[4] = "hers"
			pronounsArray[5] = "herself"
		
		} 
		elseif reference0 == "theythemtheirs" {
		
			pronounsArray = IARRAY 
			pronounsArray[0] = "plural"
			pronounsArray[1] = "they"
			pronounsArray[2] = "them"
			pronounsArray[3] = "their"
			pronounsArray[4] = "theirs"
			pronounsArray[5] = "themself"
		
		} else { //reference0 is singular or plural
			pronounsArray[0] = reference0
		}
		

	}
	--
	if reference1 == "firstboot" //This is the end of the firstboot dialogue
	{
		"\0\s[0]Alright.\w8 I will use %(subjectP), %(objectP), %(possessiveD), %(possessiveP), and %(reflexiveP) for you.\w8 You can double click on my stomach to open my menu, and access additional options under the config header.\e"
	}
	--
	"\0\s[0]Ok,\w4 I'll use %(subjectP), %(objectP), %(possessiveD), %(possessiveP), and %(reflexiveP) for you."
	--
	"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	
}

OnSubjectPronounTeach{

	//LOGGING(reference0+", "+reference1+", "+reference2+", " + reference3)
	"\t\0\s[0]\![set,balloontimeout,-1]Please enter the subject pronoun such as:\n\n\f[bold,1]They\f[bold,0] have a dog. \![open,inputbox,OnObjectPronounTeach,-1,--reference=%(reference0)]\e"

}

OnObjectPronounTeach{

	LOGGING(reference0+", "+reference1+", "+reference2+", " + reference3)
	
	if reference0 == "" {
	
		"\t\0\s[0]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the subject pronoun such as:\n\n\f[bold,1]They\f[bold,0] have a dog."
		--
		"\![open,inputbox,OnObjectPronounTeach,-1,--reference=%(reference2)]\e"
	
	}
	--
	pronounsArray[1] = reference0

	"\t\0\s[0]\![set,balloontimeout,-1]Please enter the object pronoun such as:\n\nI gave \f[bold,1]them\f[bold,0] a dog. \![open,inputbox,OnPossessiveDeterminerTeach,-1,--reference=%(reference2)]\e"

}

OnPossessiveDeterminerTeach{


	if reference0 == "" {
	
		"\t\0\s[0]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the object pronoun such as:\n\nI gave \f[bold,1]them\f[bold,0] a dog."
		--
		"\![open,inputbox,OnPossessiveDeterminerTeach,-1,--reference=%(reference2)]\e"
	
	}
	--
	pronounsArray[2] = reference0

	"\t\0\s[0]\![set,balloontimeout,-1]Please enter the posessive determiner such as:\n\nThat is \f[bold,1]their\f[bold,0] dog. \![open,inputbox,OnPossessivePronounTeach,-1,--reference=%(reference2)]\e"

}

OnPossessivePronounTeach{

	if reference0 == "" {
	
		"\t\0\s[0]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the posessive determiner such as:\n\nThat is \f[bold,1]their\f[bold,0] dog. "
		--
		"\![open,inputbox,OnPossessivePronounTeach,-1,--reference=%(reference2)]\e"
	
	}
	--
	pronounsArray[3] = reference0

	"\t\0\s[0]\![set,balloontimeout,-1]Please enter the posessive pronoun such as:\n\nThe dog is \f[bold,1]theirs\f[bold,0]. \![open,inputbox,OnReflexivePronounTeach,-1,--reference=%(reference2)]\e"

}

OnReflexivePronounTeach{

	if reference0 == "" {
	
		"\t\0\s[0]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the posessive pronoun such as:\n\nThe dog is \f[bold,1]theirs\f[bold,0]. "
		--
		"\![open,inputbox,OnReflexivePronounTeach,-1,--reference=%(reference2)]\e"
	
	}
	--
	pronounsArray[4] = reference0

	"\t\0\s[0]\![set,balloontimeout,-1]Please enter the reflexive pronoun such as:\n\nThey bought the dog \f[bold,1]themself\f[bold,0]. \![open,inputbox,OnSingularPluralTeach,-1,--reference=%(reference2)]\e"

}

OnSingularPluralTeach{

	LOGGING(reference0+", "+reference1+", "+reference2+", " + reference3)
	
	if reference0 == "" {
	
		"\t\0\s[0]\![set,balloontimeout,-1]Did you forget to enter something?\w8 Please enter the reflexive pronoun such as:\n\nThey bought the dog \f[bold,1]themself\f[bold,0]. "
		--
		"\![open,inputbox,OnSingularPluralTeach,-1,--reference=%(reference2)]\e"
	
	}
	--
	pronounsArray[5] = reference0
	
	all:{	
		"\*\0\s[0]\![set,balloontimeout,-1]Is this a singular or plural pronoun?\n\nSingular: She \f[bold,1]is\f[bold,0] kind.\nPlural: They \f[bold,1]are\f[bold,0] kind.\n\n"	
		"\![*]\q[Singular,OnPronouns,singular,%(reference2)]\n"
		"\![*]\q[Plural,OnPronouns,plural,%(reference2)]\n\e"
	}

}


//Pronoun examples for my own sake:
//subject: they
//object: them
//possessive determiner: their
//possessive pronoun: theirs 
//reflexive pronoun: themselves



OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	oldbirthday = birthday
	
	"\0\s[0]Sure.\w8 What month were you born in?\w8 Name or number is fine."
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
}

//birthday[0] - word birthday
//birthday[1] - number birthday
//birthday[2] - word month
//birthday[3] - word day
//birthday[4] - number month
//birthday[5] - number day
//Use birthday[0] to display the user's birthday, and birthday[1] to do checks with the numbers

OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	if _tempmonth == "january" || _tempmonth == "1" {birthday[4] = 1; birthday[2] = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {birthday[4] = 2; birthday[2] = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {birthday[4] = 3; birthday[2] = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {birthday[4] = 4; birthday[2] = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {birthday[4] = 5; birthday[2] = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {birthday[4] = 6; birthday[2] = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {birthday[4] = 7; birthday[2] = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {birthday[4] = 8; birthday[2] = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {birthday[4] = 9; birthday[2] = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {birthday[4] = 10; birthday[2] = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {birthday[4] = 11; birthday[2] = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {birthday[4] = 12; birthday[2] = "December"}
	else //if the month isn't a valid month, like 32 or something.
	{
		"\0\s[0]%(_tempmonth) isn't a valid month.\w8 Try again."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\*\e"
	}
	--
	//If it's a valid month
	"%(birthday[2]), okay.\w8 What day were you born?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\*\e"
}

OnUserBornDay
{
	
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	_month = birthday[4] //Just for readability
	
	if ((_month == 1 || _month == 3 || _month == 5 || _month == 7 || _month == 8 || _month == 10 || _month == 12) && _tempday >= 1 && _tempday <= 31) || (_month == 2 && _tempday >= 1 && _tempday <= 29) || ((_month == 4 || _month == 6 || _month == 9 || _month == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range.
	{
		birthday[5] = _tempday
		birthday[3] = Printday
		birthday[1] = "%(birthday[4])/%(birthday[5])" //If you want to write dates as DD/MM, feel free to swap this around
		birthday[0] = "%(birthday[2]) %(birthday[3])"
		ERASEVAR("oldbirthday")
		
		if birthday[1] == "%(month)/%(day)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
		{
			"\0\s[0]Happy birthday,\w4 %(username)."
		}
		elseif birthday[1] == "12/25" //If it's on christmas. Put any date you'd like here. If you change the format to DD/MM, make sure you change any dates like this that might be in bootend.
		{
			"\0\s[0]Oh, you were born on Christmas?\w8 Do you get twice the presents then?"
		}
		elseif birthday[1] == "1/1" 
		{
			"\0\s[0]Ah, you were born on New Year's Day.\w8 A rather auspicious day, I would think."
		}
		elseif birthday[1] == "10/31" 
		{
			"\0\s[0]Kind of a spooky birthday, hmm."
		}
		elseif birthday[1] == "8/3" 
		{
			"\0\s[0]Hm, you share a birthday with my partner, Date."
		}
		elseif birthday[1] == "10/26" 
		{
			"\0\s[0]You share a birthday with Iris.\w8 She's usually working, even on her birthday."
		}
		elseif birthday[1] == "7/6" 
		{
			"\0\s[0]You share a birthday with Mizuki.\w8 She sure is growing up fast..."
		}
		elseif birthday[1] == "12/21" 
		{
			"\0\s[0]You share a birthday with Boss.\w8 Although, I'm sure she'd argue she's stopped aging."
		}
		elseif birthday[1] == "11/9" 
		{
			"\0\s[0]You share a birthday with me!\w8 ...Yes, even though I'm an AI, I can celebrate my birthday."
		}
		elseif birthday[1] == "6/8" 
		{
			"\0\s[0]You share a birthday with Ota.\w8 Well, at least now I have a better person to associate the day with."
		}
		elseif birthday[1] == "2/1" 
		{
			"\0\s[0]You share a birthday with Pewter.\w8 That'll make it easy to remember."
		}
		else //if the birthday is not listed above.
		{
			"\0\s[0]Okay, your birthday is %(birthday[0]).\w8 I'll remember."
		}
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[0]That isn't a valid day.\w8\w8 Try again."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1]\*\e"
	}
	
}

Printday //Sets the appropriate suffix
{
	if birthday[5] == 1 || birthday[5] == 21 || birthday[5] == 31
	{
		"%(birthday[5])st"
	}
	elseif birthday[5] == 2 || birthday[5] == 22
	{
		"%(birthday[5])nd"
	}
	elseif birthday[5] == 3 || birthday[5] == 23
	{
		"%(birthday[5])rd"
	}
	else
	{
		"%(birthday[5])th"
	}
}	